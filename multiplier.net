*cfr3-(CandBandA)n -- Indipendent signal generators and Vdd power supply
*(AandBandC)n
***********
.INCLUDE 45nm_MGK.pm
***********
.OPTIONS GMIN=1e-020 ABSTOL=1e-18



*Parameter definition
.PARAM lmin=45n
.PARAM wmin=45n

*Description of the MOS



.Subckt half_adder h1 h2 hsum hcarry v 0 
X1 h1 h2 hsum v 0 xor
X2 h1 h2 hcarry v 0 and
.ends

.Subckt not a b v 0 
M1 b a 0 0 nmos W={wmin} L={lmin} 
M2 b a v v pmos W={wmin} L={lmin}
.ends 

.Subckt xor x1 x2 xout v 0 
X2 x1 x1bar v 0 not  
X1 x2 x2bar v 0 not 
M1 Vs1 x2 0 0 nmos W={wmin} L={lmin}
M2 Vs2 x2bar 0 0 nmos W={wmin} L={lmin}
M3 xout x1 Vs1 0 nmos W={wmin} L={lmin}
M4 xout x1bar Vs2 0 nmos W={wmin} L={lmin}
M5 xout x1 Vs4 v pmos W={wmin} L={lmin}
M6 xout x2 Vs4 v pmos W={wmin} L={lmin}
M7 Vs4 x1bar v v pmos W={wmin} L={lmin}
M8 Vs4 x2bar v v pmos W={wmin} L={lmin}
.ends

.Subckt nand n1 n2 nout v 0 
M1 Vs1 n2 0 0 nmos W={wmin} L={lmin}
M2 nout n1 Vs1 0 nmos W={wmin} L={lmin}
M3 nout n1 v v pmos W={wmin} L={lmin}
M4 nout n2 v v pmos W={wmin} L={lmin}
.ends

.Subckt and a1 a2 aout v 0
X1 a1 a2 out1 v 0 nand 
X2 out1 aout  v 0 not
.ends 


VD v 0 1.2
VA0 A0 0 0
VA1 A1 0 0
VB0 B0 0 0
VB1 B1 0 0
X1 A0 B0 out01 v 0 and
X2 A1 B0 out2 v 0 and
X3 A0 B1 out3 v 0 and
X4 A1 B1 out4 v 0 and
X5 out2 out3 out11 carry0 v 0 half_adder
X6 out4 carry0 out21 out31 v 0 half_adder  


.CONTROL
foreach va_temp1 0 1.2
	alter VA1=$va_temp1
	foreach va_temp0 0 1.2
		alter VA0 = $va_temp0
		foreach vb_temp1 0 1.2
			alter VB1 = $vb_temp1
			foreach vb_temp0 0 1.2
				alter VB0 = $vb_temp0
				op
				*print v(A1) v(A0) v(B1) v(B0) v(out01) v(out11) v(out21) v(out31)

				* print v(A1) v(A0) v(B1) v(B0) v(out01) v(out11) v(out21) v(out31) v(X1.out1) v(X1.X1.Vs1) v(X2.out1) v(X2.X1.Vs1) v(out2) v(X3.out1) v(X3.X1.Vs1) v(out3) v(X4.out1) v(X4.X1.Vs1) v(out4) v(X5.X2.out1) v(X5.X2.X1.Vs1) v(X5.X1.Vs1) v(X5.X1.Vs2) v(X5.X1.Vs4) v(carry0) v(X6.X2.out1) v(X6.X2.X1.Vs1) v(X6.X1.Vs1) v(X6.X1.Vs2) v(X6.X1.Vs4) 
            
				set wr_vecnames
				set wr_singlescale
				set appendwrite 
				wrdata res.txt v(A1) v(A0) v(B1) v(B0) v(out01) v(out11) v(out21) v(out31) v(X1.out1) v(X1.X1.Vs1) v(X2.out1) v(X2.X1.Vs1) v(out2) v(X3.out1) v(X3.X1.Vs1) v(out3) v(X4.out1) v(X4.X1.Vs1) v(out4) v(X5.X2.out1) v(X5.X2.X1.Vs1) v(X5.X1.Vs1) v(X5.X1.Vs2) v(X5.X1.Vs4) v(carry0) v(X6.X2.out1) v(X6.X2.X1.Vs1) v(X6.X1.Vs1) v(X6.X1.Vs2) v(X6.X1.Vs4)  
			end
		end
	end
end
.endc
.end